apply plugin: 'pmd'

pmd {
    toolVersion = '6.20.0'
    ignoreFailures = !project.failOnStaticChecks
    consoleOutput = false
}

tasks.withType(Pmd) {
    if (name == 'pmdMain') {
        ruleSetFiles = files("${rootProject.projectDir}/config/pmd/ruleSetMain.xml")
        ruleSets = []
    } else {
        ruleSetFiles = files("${rootProject.projectDir}/config/pmd/ruleSetTest.xml")
        ruleSets = []
    }
}

apply plugin: 'checkstyle'

checkstyle {
    toolVersion = '8.28'
    ignoreFailures = !project.failOnStaticChecks
    showViolations = false
}

tasks.withType(Checkstyle) {
    if (name == 'checkstyleMain') {
        //noinspection GroovyAssignabilityCheck
        configFile = file("${rootProject.projectDir}/config/checkstyle/checkstyleMain.xml")
    } else {
        //noinspection GroovyAssignabilityCheck
        configFile = file("${rootProject.projectDir}/config/checkstyle/checkstyleTest.xml")
    }
}

apply plugin: 'codenarc'

codenarc {
    toolVersion = '1.6'
    ignoreFailures = !project.failOnStaticChecks
}

tasks.withType(CodeNarc) {
    if (name == 'codenarcTest') {
        configFile = file('config/codenarc/codenarcTest.properties')
    } else {
        configFile = file('config/codenarc/codenarcMain.properties')
        source file('build.gradle')
        source file('gradle/validation.gradle')
        source file('gradle/coverage.gradle')
        source file('gradle/ideaAnnotationProcessor.gradle')
    }
    logging.level = LogLevel.ERROR
}

dependencies {
    checkstyle 'com.thomasjensen.checkstyle.addons:checkstyle-addons:4.0.0'
}
